import groovy.io.FileType
import hudson.model.Item
import javaposse.jobdsl.dsl.DslScriptLoader
import javaposse.jobdsl.dsl.GeneratedItems
import javaposse.jobdsl.dsl.GeneratedJob
import javaposse.jobdsl.dsl.JobManagement
import javaposse.jobdsl.plugin.JenkinsJobManagement
import jenkins.model.Jenkins
import org.junit.ClassRule
import org.jvnet.hudson.test.JenkinsRule
import spock.lang.Shared
import spock.lang.Specification
import spock.lang.Unroll

class BuildJobSpec extends Specification {

    @Shared
    @ClassRule
    private JenkinsRule jenkinsRule = new JenkinsRule()

    @Shared
    private File outputDir = new File('./build/debug-xml')

    def setupSpec() {
        outputDir.deleteDir()
    }

    boolean ensureEnvironment(def files) {
        boolean match = true
        files.each {
            match &= it.text.contains("<propertiesContent>MSB_OPTIONS=/property:Configuration=x86</propertiesContent>")
        }
        return match
    }

    @Unroll
    void 'test script #file.name'(File file) {
        given:
        JobManagement jm = new JenkinsJobManagement(System.out, [:], new File('.'))

        when:
        GeneratedItems items = new DslScriptLoader(jm).runScript(file.text)
        def writtenItems = writeItems(items)

        then:
        noExceptionThrown()
        ensureEnvironment(writtenItems)

        where:
        file << jobFiles
    }

    /**
     * Write the config.xml for each generated job and view to the build dir.
     */
    def writeItems(GeneratedItems items) {
        def files = []
        Jenkins jenkins = jenkinsRule.jenkins

        items.jobs.each { GeneratedJob generatedJob ->
            String jobName = generatedJob.jobName
            Item item = jenkins.getItemByFullName(jobName)
            String text = new URL(jenkins.rootUrl + item.url + 'config.xml').text
            files << writeFile(new File(outputDir, 'jobs'), jobName, text)
        }
        return files
    }

    /**
     * Write a single XML file, creating any nested dirs.
     */
    File writeFile(File dir, String name, String xml) {
        List tokens = name.split('/')
        File folderDir = tokens[0..<-1].inject(dir) { File tokenDir, String token ->
            new File(tokenDir, token)
        }
        folderDir.mkdirs()

        File xmlFile = new File(folderDir, "${tokens[-1]}.xml")
        xmlFile.text = xml
        return xmlFile
    }

    static List<File> getJobFiles() {
        List<File> files = []
        new File('jobs').eachFileRecurse(FileType.FILES) {
            if (it.name.endsWith('.groovy')) {
                files << it
            }
        }
        files
    }
}
